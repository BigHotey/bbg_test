
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Попытка
		НачатьТранзакцию();
		Последовательности.ПоследовательностьНакладных.ПолучитьГраницу();
	
		Движения.ТоварыНаСкладах.Записывать = Истина;
		Движения.ТоварыНаСкладах.Очистить();
		
		ТЗОстатокКСписанию = СписаниеПоСкладамТЧ();
		ТЗКонечныйОстаток = СписаниеСоСкладовПоПриоритетам(ТЗОстатокКСписанию);
		
		Если ТЗКонечныйОстаток.Количество() > 0 Тогда
			Сообщить("Не достаточно товара на складах.");
			ОтменитьТранзакцию();
		КонецЕсли;
				
		Движения.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры

Функция СписаниеПоСкладамТЧ()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	|	РасходнаяНакладнаяТовары.Склад КАК Склад,
	|	РасходнаяНакладнаяТовары.Количество КАК Количество
	|ПОМЕСТИТЬ втРасходнаяНакладнаяТовары
	|ИЗ
	|	&ТЗДокТЧ КАК РасходнаяНакладнаяТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	|	РасходнаяНакладнаяТовары.Склад КАК Склад,
	|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ втДокТЧ
	|ИЗ
	|	втРасходнаяНакладнаяТовары КАК РасходнаяНакладнаяТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяТовары.Номенклатура,
	|	РасходнаяНакладнаяТовары.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РасходнаяНакладнаяТовары.Склад,
	|	РасходнаяНакладнаяТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокТЧ.Номенклатура,
	|	ДокТЧ.Склад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - ДокТЧ.Количество < 0
	|			ТОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0)
	|		ИНАЧЕ ДокТЧ.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - ДокТЧ.Количество <= 0
	|			ТОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.СтоимостьОстаток, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТоварыНаСкладахОстатки.СтоимостьОстаток, 0) / ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) * ДокТЧ.Количество
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - ДокТЧ.Количество < 0
	|			ТОГДА ДокТЧ.Количество - ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОстатокКСписаниюКоличество
	|ПОМЕСТИТЬ втДвижения
	|ИЗ
	|	втДокТЧ КАК ДокТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаДокумента, ) КАК ТоварыНаСкладахОстатки
	|		ПО ДокТЧ.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|			И ДокТЧ.Склад = ТоварыНаСкладахОстатки.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДвижения.Номенклатура,
	|	втДвижения.Склад,
	|	втДвижения.Количество,
	|	втДвижения.Стоимость
	|ИЗ
	|	втДвижения КАК втДвижения
	|ГДЕ
	|	втДвижения.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДвижения.Номенклатура,
	|	втДвижения.Склад,
	|	втДвижения.ОстатокКСписаниюКоличество КАК Количество
	|ИЗ
	|	втДвижения КАК втДвижения
	|ГДЕ
	|	втДвижения.ОстатокКСписаниюКоличество > 0";
	Запрос.УстановитьПараметр("ТЗДокТЧ", Товары.Выгрузить());
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Рез = Запрос.ВыполнитьПакет();
	
	ТЗОстатокКСписанию = Рез[Рез.Количество()-1].Выгрузить();
	ТЗДвижения = Рез[Рез.Количество()-2].Выгрузить();
	
	Для Каждого СтрДвижения ИЗ ТЗДвижения Цикл
		НовыйРасход = Движения.ТоварыНаСкладах.ДобавитьРасход();
		НовыйРасход.Период = Дата;
		ЗаполнитьЗначенияСвойств(НовыйРасход, СтрДвижения);
	КонецЦикла;
		
	Возврат ТЗОстатокКСписанию;
КонецФункции

Функция СписаниеСоСкладовПоПриоритетам(ТЗОстатокКСписанию)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЗОстатокКСписанию.Номенклатура,
	|	ТЗОстатокКСписанию.Склад,
	|	ТЗОстатокКСписанию.Количество
	|ПОМЕСТИТЬ втОстатокКСписанию
	|ИЗ
	|	&ОстатокКСписанию КАК ТЗОстатокКСписанию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаток.Номенклатура,
	|	втОстаток.Склад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - втОстаток.Количество < 0
	|			ТОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0)
	|		ИНАЧЕ втОстаток.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - втОстаток.Количество <= 0
	|			ТОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.СтоимостьОстаток, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТоварыНаСкладахОстатки.СтоимостьОстаток, 0) / ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) * втОстаток.Количество
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - втОстаток.Количество < 0
	|			ТОГДА втОстаток.Количество - ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОстатокКСписаниюКоличество
	|ПОМЕСТИТЬ втДвижения
	|ИЗ
	|	втОстатокКСписанию КАК втОстаток
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаДокумента, Склад = &Склад) КАК ТоварыНаСкладахОстатки
	|		ПО втОстаток.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|			И (втОстаток.Склад <> &Склад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДвижения.Номенклатура,
	|	&Склад КАК Склад,
	|	втДвижения.Количество,
	|	втДвижения.Стоимость
	|ИЗ
	|	втДвижения КАК втДвижения
	|ГДЕ
	|	втДвижения.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДвижения.Номенклатура,
	|	втДвижения.Склад,
	|	втДвижения.ОстатокКСписаниюКоличество КАК Количество
	|ИЗ
	|	втДвижения КАК втДвижения
	|ГДЕ
	|	втДвижения.ОстатокКСписаниюКоличество > 0";
	
	ВыборкаСкладов = РегистрыСведений.ПриоритетыСкладов.ПолучитьВыборку(Дата);
	Пока ВыборкаСкладов.Следующий() Цикл
		Запрос.УстановитьПараметр("ОстатокКСписанию", ТЗОстатокКСписанию);
		Запрос.УстановитьПараметр("ДатаДокумента", Дата);
		Запрос.УстановитьПараметр("Склад", ВыборкаСкладов.Склад);
		
		Рез = Запрос.ВыполнитьПакет();
		
		ТЗОстатокКСписанию = Рез[Рез.Количество()-1].Выгрузить();
		ТЗДвижения = Рез[Рез.Количество()-2].Выгрузить();
		
		Для Каждого СтрДвижения ИЗ ТЗДвижения Цикл
			НовыйРасход = Движения.ТоварыНаСкладах.ДобавитьРасход();
			НовыйРасход.Период = Дата;
			ЗаполнитьЗначенияСвойств(НовыйРасход, СтрДвижения);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТЗОстатокКСписанию;
	
КонецФункции
